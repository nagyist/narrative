plugins {
    id "java-library"
    id "maven-publish"
    id "com.timgroup.jarmangit" version "1.1.86"
}

ext {
    buildNumber = System.getenv("BUILD_NUMBER")
}

group = "com.youdevise"
if (buildNumber) version = "1.0.$buildNumber"

dependencies {
    implementation "org.hamcrest:hamcrest-integration:1.2"

    testImplementation "org.jmock:jmock:2.5.1"
    testImplementation "org.jmock:jmock-junit4:2.5.1"
    testImplementation "junit:junit:4.12"
}

tasks.withType(Jar).all {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

tasks.withType(JavaCompile).all {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.deprecation = true
    options.compilerArgs << "-parameters"
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    maven {
        url = "$repoUrl/groups/public"
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

assemble.dependsOn(javadocJar)

publishing {
    repositories {
        maven {
            url = "$repoUrl/repositories/yd-release-candidates"
            credentials {
                username = project.repoUsername
                password = project.repoPassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
}
